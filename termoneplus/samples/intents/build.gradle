plugins {
    id 'com.android.application'
}

// "TermOne Plus" applicationId
def termApplicationId = "com.termoneplus"

android {
    namespace "com.termoneplus.sample.intents"

    compileSdk rootProject.ext.compileSdk
    buildToolsVersion = rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.termoneplus.sample.intents"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode 210
        versionName '2.1.0'

        manifestPlaceholders.applicationIdSuffix = ""
        buildConfigField("String", "APPLICATION_ID_SUFFIX", "\"\"")

        manifestPlaceholders.termApplicationId = "${termApplicationId}"
        buildConfigField("String", "TERM_APPLICATION_ID", "\"${termApplicationId}\"")
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    productFlavors {
    }
    buildTypes {
        debug {
            applicationIdSuffix ".devel"

            // NOTE: used in third party development applications
            manifestPlaceholders.applicationIdSuffix = "${applicationIdSuffix}"
            buildConfigField("String", "APPLICATION_ID_SUFFIX", "\"${applicationIdSuffix}\"")
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.material
    implementation libs.annotation
}
